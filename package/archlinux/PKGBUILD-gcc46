# Author: mosra <mosra@centrum.cz>
pkgname=magnum-plugins
pkgver=dev.gcc46
pkgrel=1
pkgdesc="Plugins for Magnum OpenGL graphics engine (built with GCC 4.6)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('magnum')
makedepends=('gcc46' 'cmake' 'ninja' 'qt4' 'freetype2' 'harfbuzz' 'libjpeg' 'libpng')
options=(!strip)
provides=('magnum-plugins-git')

_rootdir=$startdir/../../

build() {
    # Disable flags unknown to GCC 4.7
    newcxxflags=$(echo $CXXFLAGS | sed s/-fstack-protector-strong.//g)
    export CXXFLAGS="$newcxxflags"

    if [ ! -d "$_rootdir/build-gcc46" ] ; then
        mkdir "$_rootdir/build-gcc46"
        cd "$_rootdir/build-gcc46"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-4.6 \
            -G Ninja
    fi

    cd "$_rootdir/build-gcc46"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_ANYSCENEIMPORTER=ON \
        -DWITH_COLLADAIMPORTER=ON \
        -DWITH_FREETYPEFONT=ON \
        -DWITH_HARFBUZZFONT=ON \
        -DWITH_JPEGIMPORTER=ON \
        -DWITH_PNGIMPORTER=ON \
        -DWITH_OPENGEXIMPORTER=ON \
        -DWITH_STANFORDIMPORTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBPNGIMAGECONVERTER=ON
    ninja
}

check() {
    cd "$_rootdir/build-gcc46"
    ctest --output-on-failure
}

package() {
  cd "$_rootdir/build-gcc46"
  DESTDIR="$pkgdir/" ninja install
}
