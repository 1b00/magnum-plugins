# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum-plugins
pkgver=dev
pkgrel=1
pkgdesc="Plugins for Magnum OpenGL graphics engine (Emscripten)"
arch=('any')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('emscripten-magnum')
makedepends=('cmake' 'emscripten' 'corrade' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten" ] ; then
        mkdir "$_rootdir/build-emscripten"
        cd "$_rootdir/build-emscripten"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DWITH_ANYAUDIOIMPORTER=ON \
        -DWITH_ANYIMAGECONVERTER=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_ANYSCENEIMPORTER=ON \
        -DWITH_DDSIMPORTER=ON \
        -DWITH_DRFLACAUDIOIMPORTER=ON \
        -DWITH_DRWAVAUDIOIMPORTER=ON \
        -DWITH_MINIEXRIMAGECONVERTER=ON \
        -DWITH_OPENGEXIMPORTER=ON \
        -DWITH_STANFORDIMPORTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBPNGIMAGECONVERTER=ON \
        -DWITH_STBTRUETYPEFONT=ON \
        -DWITH_STBVORBISAUDIOIMPORTER=ON \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$_rootdir/build-emscripten"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten"
    DESTDIR="$pkgdir/" ninja install
}
